plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'java'
}

group = 'de.paradubsch.paradubschmanager'
version = '1.2'

repositories {
    mavenCentral()
    maven { url 'https://papermc.io/repo/repository/maven-public/' }
    maven { url 'https://oss.sonatype.org/content/groups/public/' }
    maven { url 'https://maven.enginehub.org/repo/' }
    maven { url 'https://repo.dmulloy2.net/repository/public/' }
    maven { url 'https://hub.jeff-media.com/nexus/repository/jeff-media-public/' }
    maven { url 'https://repo.codemc.io/repository/maven-public/' }
    maven { url 'https://libraries.minecraft.net/' }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.19.2-R0.1-SNAPSHOT'
    compileOnly 'org.projectlombok:lombok:1.18.26'
    compileOnly 'com.arcaniax:HeadDatabase-API:1.3.1'
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.6'
    compileOnly 'com.sk89q.worldedit:worldedit-bukkit:7.2.6'
    compileOnly 'com.comphenix.protocol:ProtocolLib:4.8.0'
    compileOnly 'net.luckperms:api:5.4'
    compileOnly 'net.raidstone:WorldGuardEvents:1.18.1'
    compileOnly 'mysql:mysql-connector-java:8.0.31'
    compileOnly 'org.eclipse.jetty:jetty-server:11.0.12'
    compileOnly 'org.apache.commons:commons-lang3:3.3.2'

    annotationProcessor 'org.projectlombok:lombok:1.18.26'

    implementation 'org.hibernate:hibernate-core:5.6.14.Final'
    implementation 'org.hibernate:hibernate-ehcache:5.4.27.Final'
    implementation 'org.hibernate:hibernate-c3p0:5.4.27.Final'
    implementation 'me.lucko:commodore:2.2'
    implementation 'com.jeff_media:CustomBlockData:2.1.0'

    testImplementation 'io.papermc.paper:paper-api:1.19.2-R0.1-SNAPSHOT'
    testImplementation 'com.arcaniax:HeadDatabase-API:1.3.1'
    testImplementation 'com.sk89q.worldguard:worldguard-bukkit:7.0.6'
    testImplementation 'com.sk89q.worldedit:worldedit-bukkit:7.2.6'
    testImplementation 'com.comphenix.protocol:ProtocolLib:4.8.0'
    testImplementation 'net.luckperms:api:5.4'
    testImplementation 'net.raidstone:WorldGuardEvents:1.18.1'
    testImplementation 'mysql:mysql-connector-java:8.0.31'
    testImplementation 'org.eclipse.jetty:jetty-server:11.0.12'
    testImplementation 'org.apache.commons:commons-lang3:3.3.2'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testImplementation 'com.github.seeseemelk:MockBukkit-v1.19:2.110.0'

    testCompileOnly 'org.projectlombok:lombok:1.18.26'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.26'
}

tasks {
    processResources {
        def props = [version: version]
        inputs.properties props
        filteringCharset 'UTF-8'
        filesMatching('plugin.yml') {
            expand props
        }
    }

    shadowJar {
        archiveClassifier.set(null)
        exclude("META-INF/**")

        relocate 'com.jeff_media.customblockdata', 'de.craftery.lib.relocations.customblockdata'
        relocate 'me.lucko.commodore', 'de.craftery.lib.relocations.commodore'
    }

    build {
        dependsOn(shadowJar)
    }
    jar {
        archiveClassifier.set("unshaded")
    }
    test {
        useJUnitPlatform()
    }
}